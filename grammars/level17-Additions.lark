// We add the : at the end of if and else and elif and for/while

command: print | ifs (elifs)* elses? | ask | for_loop | repeat_list | while_loop| assign_list | add | remove | sleep | list_access_var | assign | comment

//spaces are allowed before and after colon
//TODO: this could be an attribute on the rule that we add in the merger
_COLON: _SPACE* ":" _SPACE*

elses: _EOL _SPACE* _ELSE _COLON _EOL (_SPACE command) (_EOL _SPACE command)* _EOL _END_BLOCK
//TODO: after condition a space (or more) could also be ok? Python allows that
ifs: _IF _SPACE condition _COLON _EOL (_SPACE command) (_EOL _SPACE command)* _EOL _END_BLOCK//'if' cannot be used in Python, hence the name of the rule is 'ifs'
elifs: _EOL _SPACE* _ELIF _SPACE condition _COLON _EOL (_SPACE command) (_EOL _SPACE command)* _EOL _END_BLOCK

for_loop: _FOR _SPACE (NAME | var) _SPACE _IN _SPACE _RANGE _SPACE (INT | var) _SPACE _TO _SPACE (INT | var) _COLON _EOL (_SPACE command) (_EOL _SPACE command)* _EOL _END_BLOCK
while_loop: _WHILE _SPACE (condition) _COLON _EOL (_SPACE command) (_EOL _SPACE command)* _EOL _END_BLOCK
repeat_list: _FOR _SPACE var _SPACE _IN _SPACE var _COLON _EOL (_SPACE command) (_EOL _SPACE command)* _EOL _END_BLOCK


//SYMBOLS
_SPACE: " "
_COMMA: ","
_COLON: ":"

_PRINT : "print"
_ASK : "ask"
_ECHO : "echo"

//level 2
_IS: "is"

//level 4
_IN: "in"
_IF: "if"
_ELSE: "else"
_AND: "and"
_AT: "at"

//level 5
_REPEAT: "repeat"
_TIMES: "times"

//level 6
_MULTIPLY: "*"
_ADD: "+"
_SUBTRACT: "-"
_DIVIDE: "/"

//level 8
_FOR: "for"
_RANGE: "range"
_TO: "to"
_STEP: "step"

//level 9
_ELIF: "elif"



start: program
program: _EOL* command (_SPACE)* (_EOL+ command (_SPACE)*)* _EOL* //lines may end on spaces and might be separated by many newlines
command: print | ask | echo | invalid_space | invalid

print: _PRINT (_SPACE text)?
ask: _ASK (_SPACE text)?
echo: _ECHO (_SPACE text)?
invalid_space: _SPACE+ text
invalid: textwithoutspaces text?

_EOL: "\r"?"\n"

text: /([^\n]+)/ -> text //anything can be parsed except for a newline
textwithoutspaces: /([^\n *+-\/]+)/ -> text //anything can be parsed except for spaces (plus: a newline)

%import common.LETTER
%import common.DIGIT
%import common.WS_INLINE
%import common.NEWLINE
%import common.SIGNED_INT -> NUMBER
%import common.CNAME -> NAME

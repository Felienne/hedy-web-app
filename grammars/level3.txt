start: program
program: command ("\n" command)*
command: "print " (quoted_text | list_access | var) (" " (quoted_text | list_access | var))*  -> print
         | "ask " textwithspaces punctuation* -> ask
         | "echo " textwithspaces punctuation* -> echo
         | text " is " text ((", "|",") text)+  -> assign_list
         | text " is " text -> assign //placing it here means print is will print 'is' and print is Felienne will print 'is Felienne'
         | text " " textwithspaces -> invalid

var: LETTER+ -> var
list_access : var " at " (index | random) -> list_access
index : DIGIT+
random : "random"
punctuation : PUNCTUATION -> punctuation
textwithspaces: (LETTER | DIGIT | WS_INLINE)+ -> text
quoted_text: QUOTE (LETTER | DIGIT | PUNCTUATION | WS_INLINE)+ QUOTE -> text
QUOTE : "'"
text: (LETTER | DIGIT)+ -> text
PUNCTUATION: "!" | "?" | "." //uppercase places tokens in tree

%import .grammars.common.LETTER   // imports from terminal library
%import .grammars.common.DIGIT   // imports from terminal library
%import .grammars.common.WS_INLINE   // imports from terminal library
%import .grammars.common.NEWLINE   // imports from terminal library




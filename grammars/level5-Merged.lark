start: program
program: _EOL* (command | invalid) (" ")* (_EOL+ (command | invalid) (" ")*)* _EOL* //lines may end on spaces and might be separated by many newlines
command: print | repeat | ifelse | ifs | ask | assign_list | list_access_var | assign | invalid
print: "print " (quoted_text | list_access | var) (" " (quoted_text | list_access | var))*
ask: var " is ask " textwithspaces*
echo: "echo" (" " text)?
invalid_space: " "+ text
invalid: textwithoutspaces " " textwithspaces
_EOL: "\r"?"\n"
text: /([^\n,]+)/ -> text //anything can be parsed except for a newline and a comma for list separators
textwithoutspaces: /([^\n, ]+)/ -> text //anything can be parsed except for spaces (plus: a newline and a comma for list separators)
SPACE: " "
%import common.LETTER
%import common.DIGIT
%import common.WS_INLINE
%import common.NEWLINE
%import common.SIGNED_INT -> NUMBER
%import common.CNAME -> NAME
assign: var " is " textwithspaces
assign_list: var " is " textwithspaces ((", "|",") textwithspaces)+
index : NUMBER //different why?
random : "random"
punctuation : PUNCTUATION -> punctuation
PUNCTUATION: "!" | "?" | "." //uppercase places tokens in tree
list_access : var " at " (index | random) -> list_access //todo: could be merged with list_access_var?
var: NAME -> var
print_no_quotes: "print " (textwithoutspaces | list_access | var) (" " (textwithoutspaces | list_access | var))*  -> print_nq
quoted_text_no_escape: /'([^']*)'/ -> text //simply all between quotes should this be used at earlier levels?
quoted_text: /'((?:[^\\']|\\.)*)'/ -> text //text can be between single quotes, but quotes may be escaped with \
ifelse: "if " condition " " command " else " command
ifs: "if " condition " " command //'if' cannot be used in Python, hence the name of the rule is 'ifs'
condition: (equality_check|in_list_check) (" and " condition)*
list_access_var: var " is " var " at " (index | random)
equality_check: textwithoutspaces " is " textwithoutspaces
in_list_check: textwithoutspaces " in " var
textwithspaces: /([^\n,]+)/ -> text //anything can be parsed except for a newline and a comma for list separators
repeat: "repeat " (NUMBER | var) " times " command

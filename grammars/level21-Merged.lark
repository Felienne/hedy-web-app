%import common.CNAME -> NAME
%import common.DIGIT
%import common.LETTER
%import common.NEWLINE
%import common.SIGNED_INT -> NUMBER
%import common.WS_INLINE
?atom: NUMBER | var | NAME | list_access
?product: atom | product " "* "*" " "* atom -> multiplication | product " "* "/" " "* atom -> division
?sum: product | sum " "* "+" " "* product -> addition | sum " "* "-" " "* product -> substraction
PUNCTUATION: "!" | "?" | "." //uppercase places tokens in tree
_ASK : "ask"
_COMMA: ","
_ECHO : "echo"
_EOL: "\r"?"\n"
_IS: "is"
_PRINT : "print"
_SPACE: " "
andcondition: (equality_check|in_list_check) (" and " condition)*
ask: var " is ask " textwithspaces*
assign: var " = " length | var " = " sum | var " = " textwithoutspaces | list_access " = " sum | list_access " = " textwithoutspaces
assign_list: var " = [" quoted_text ((", "|",") quoted_text)+ "]"
bigger: (textwithoutspaces | list_access | length) " > " (textwithoutspaces | list_access | length)
change_list_item: var "[" (index | var) "] = " (var | textwithoutspaces)
command: print | ifs (elifs)* elses? | input | for_loop | while_loop | assign_list | list_access_var | change_list_item| comment | assign
comment: "#" (textwithspaces)*
condition: (equality_check|in_list_check|smaller|bigger|not_equal)
echo: _ECHO (_SPACE text)?
elifs: _EOL (" ")* "elif " (condition|andcondition|orcondition) ":"_EOL (" "+ command) (_EOL " "+ command)* _EOL "end-block"
elses: _EOL (" ")* "else"":" _EOL (" "+ command) (_EOL " "+ command)* _EOL "end-block"
equality_check: (length | textwithoutspaces | list_access | sum) " == " (length | textwithoutspaces | list_access | sum)
for_loop: "for " (NAME | var) " in " "range(" (NUMBER | var | length)(", "|",") (NUMBER | var | length) "):" _EOL (" "+ command) (_EOL " "+ command)* _EOL "end-block"
ifelse: "if " condition " " command " else " command
ifs: "if " (condition|andcondition|orcondition) ":" _EOL (" "+ command) (_EOL " "+ command)* _EOL "end-block" //'if' cannot be used in Python, hence the name of the rule is 'ifs'
in_list_check: textwithoutspaces " in " var
index : NUMBER //different why?
input: var " = input("  (quoted_text | list_access | var | sum | length) (" " (quoted_text | list_access | var | sum | length))*  ")"
invalid: textwithoutspaces " " textwithspaces
invalid_space: _SPACE+ text
length: "length(" var ")"
list_access: var "[" (index | random | var | length) "]"
list_access_var: var " = " var "[" (index | random | var) "]"
not_equal: (length | textwithoutspaces | list_access) " != " (length | textwithoutspaces | list_access)
orcondition: (equality_check|in_list_check) (" or " condition)*
print: "print(" (quoted_text | list_access | var | sum | length) (" " (quoted_text | list_access | var | sum | length))* ")"
print_no_quotes: "print " (textwithoutspaces | list_access | var) (" " (textwithoutspaces | list_access | var))*  -> print_nq
program: _EOL* command (" ")* (_EOL+ command (" ")*)* _EOL* //lines may end on spaces and might be separated by many newlines
punctuation : PUNCTUATION -> punctuation
quoted_text: /'((?:[^\\']|\\.)*)'/ -> text //text can be between single quotes, but quotes may be escaped with \
quoted_text_no_escape: /'([^']*)'/ -> text //simply all between quotes should this be used at earlier levels?
random : "random"
repeat: "repeat " (NUMBER | var) " times" _EOL (" "+ command) (_EOL " "+ command)*
smaller: (textwithoutspaces | list_access | length) " < " (textwithoutspaces | list_access | length)
start: program
text: /([^\n,]+)/ -> text //anything can be parsed except for a newline and a comma for list separators
textwithoutspaces: /([^\n, :*+-\/]+)/ -> text
textwithspaces: /([^\n,]+)/ -> text //anything can be parsed except for a newline and a comma for list separators
var: NAME -> var
while_loop: "while " (condition|andcondition|orcondition) ":" _EOL (" "+ command) (_EOL " "+ command)* _EOL "end-block"
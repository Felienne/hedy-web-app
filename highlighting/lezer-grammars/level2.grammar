@top Program { eol* (Command eol+)* Command? }
Command {
    Assign | Ask | Print | Turtle | Sleep
}

Assign { Text is+ Text+ }
Ask { Text is+ ask+ Text+ }
Print { print+ Text+ }
Sleep { sleep+ Text }

Turtle[@isGroup=turtle] {
    Forward { forward+ Text } |
    Turn { turn+ Text } |
    Color { color+ Text }
}

ErrorInvalid[@dynamicPrecedence=-10] { Text+ }

@external specialize { Text } specializeKeyword from "./tokens" {
    ask[@name="ask"]
}

@external extend { Text } extendKeyword from "./tokens" {
    print[@name="print"],
    forward[@name="forward"],
    turn[@name="turn"],
    color[@name="color"],
    sleep[@name="sleep"],
    is[@name="is"]
}

@tokens {
    Comment { "#" ![\n]* }
    eol { "\n" }
    space { " " }
    Text { ![\n #]+ }
}

@skip { Comment | space }
@top Program { eol* (Command eol+)* Command? }

Command {
    Assign | AssignList | Input | Clear |Print | Turtle | Sleep | Add | Remove | If | Else | Repeat | For | Define | Call | Return | While | Elif |ErrorInvalid
}

IfLessCommand {
    Assign | AssignList | Input | Clear |Print | Turtle | Sleep | Add | Remove | Repeat | For | Define | Call | Return | While | Elif | ErrorInvalid
}

@local tokens {    
    stringEnd { "'" | "\n" }    
    @else stringContent
  }
  
  @skip {} {
    String { "'" (stringContent)* stringEnd }
}


Print { kw<"print">  Op<"("> Arguments? Op<")"> }

AssignList { Text (kw<"is"> | Op<"=">) Op<"["> (String | Number ) ~ambig  (Op<","> (String | Number ))+ Op<"]">} // Op<","> doesn't really parse well if it is not separated by spaces
Assign { (Text | ListAccess) (kw<"is"> | Op<"=">) (Expression ~ambig )+ }
Input { Text (kw<"is"> | Op<"=">) kw<"input"> Op<"("> Arguments? Op<")"> }
Sleep { kw<"sleep"> (Expression | Call)? }

ListAccess { Text Op<"["> (kw<"random"> | Text) Op<"]">}

Turtle[@isGroup=turtle] {
    Forward { kw<"forward"> (Expression | Call) } |
    Turn { kw<"turn"> (Expression | Call) } |
    Color { kw<"color"> (Expression | Call)  }
}   

Add { kw<"add"> (Expression | Call ) kw<"to"> Text }
Remove { kw<"remove"> (Expression | Call) kw<"from"> Text }
Clear { kw<"clear"> }
ErrorInvalid[@dynamicPrecedence=-10] { Text+ }

If { kw<"if"> Condition ((kw<"and"> | kw<"or">) Condition)* Op<":"> IfLessCommand? Else? }
Else { kw<"else"> Op<":"> IfLessCommand? }
Elif { kw<"elif"> Condition ((kw<"and"> | kw<"or">) Condition)* Op<":"> }
Condition {    
    PressedCheck { Text ~cond kw<"is"> kw<"pressed"> } |
    EqualityCheck { (Expression | Call) (kw<"is"> | Op<"="> Op<"=">? ) (Expression | Call) } |
    NotEqualCheck { (Expression | Call) (Op<"!=">) (Expression | Call) } |
    ComparisonCheck { (Expression | Call) (Op<">"> | Op<"<">) Op<"=">? (Expression | Call) } |
    ListCheck { (Expression ~cond | Call) kw<"not">? kw<"in">  Text }
}


Repeat { kw<"repeat"> (Number | Text) kw<"times"> IfLessCommand }

For { kw<"for"> Text kw<"in"> (Text | kw<"range"> Op<"("> (Number | Text) Op<","> (Number | Text) (Op<","> (Number | Text))? Op<")">) Op<":"> }
While { kw<"while"> Condition ((kw<"and"> | kw<"or">) Condition)* Op<":">}
Define { kw<"def"> Text Op<"("> Arguments? Op<")"> Op<":">}
Call { Text Op<"("> Arguments? Op<")"> }
Return { kw<"return"> ( Expression | Call)+ }


Arguments { argument (Op<","> argument)* }
argument { Expression | Call}


kw<term> { @specialize[@name={term}]<Text, term> }
skw<term> { @extend[@name={term}]<Text, term> }
Op<expr> { expr }


atom { Number | Text }

binaryExpression {
    Expression !times Op<"*" | "/"> Expression |
    Expression !plus Op<"+" | "-"> Expression
}

// I should add Call here, but it works for now
Expression {
    Number |
    Text |
    String |
    ListAccess |
    binaryExpression
}

@precedence {
    times @left,
    plus @right
}


@tokens {
    @precedence {
        Number,
        Text
    }
    Comment { "#" ![\n]* }
    eol { "\n" }
    Text { ![^\n,،，、 '+#/*\-=><!\]\[:()]+ } // a little bit of a hack not to parse commas never, but lezer adds them as error nodes, so it still
    space { " " }
    Number { @digit+ ("." @digit+)? }
}

@skip { Comment | space }